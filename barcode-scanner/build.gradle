apply plugin: 'com.android.library'

apply from: gradleScript("root/versioning-utils.gradle")
apply from: gradleScript("android/publishing-utils.gradle")

publishingUtils.publication {
    groupId "com.aevi.barcode"
    artifactId "barcode-scanner"
    version versioningUtils.versionName
}

android {
    compileSdkVersion project.compileSdkVersion
    buildToolsVersion project.buildToolsVersion
    ndkVersion project.ndkVersion
    
    defaultConfig {
        minSdkVersion projectMinSdkVersion
        targetSdkVersion projectMaxSdkVersion
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    
    // Encapsulates your external native build configurations.
    externalNativeBuild {

        // Encapsulates your CMake build configurations.
        cmake {
            // Provides a relative path to your CMake build script.
            path "CMakeLists.txt"
        }
    }

    lintOptions {
        disable 'MissingPermission'
    }
}

dependencies {
    implementation domainDependencies.rxAndroid
    testImplementation testDependencies.junit
    testImplementation testDependencies.mockito
}

project.ext.cmakedir = file("${project.buildDir}/cmake")

task cmake(type: Exec) {
    executable = 'cmake'
    args = [
            "-B" + project.cmakedir
    ]
    doFirst {
        cmakedir.mkdirs()
    }
}

task cmakeBuild(type: Exec, dependsOn: cmake) {
    executable = 'cmake'
    args = [
            '--build', project.cmakedir
    ]
}

tasks.withType(Test) {
    systemProperty "java.library.path", project.cmakedir
    dependsOn cmakeBuild
}