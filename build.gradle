// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:3.5.2"
    }
}

apply from: 'dependencies.gradle'

subprojects {

    tasks.withType(Test) {
        testLogging {
            events "started", "passed", "skipped", "failed"
        }
    }
}

allprojects {

    ext.applyScript = { String script ->
        try {
            apply from: new File(rootDir, "gradle-scripts/${script}.gradle").path
        } catch (Exception e) {
            ext."${script}" = null
            project.logger.log(LogLevel.WARN, e.message)
        }
    }

    project.ext {
        branch = 'git rev-parse --abbrev-ref HEAD'.execute([], project.rootDir).text.trim()
        sha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
        versionName = "2.2.0"
        versionCode = 2007
        apiRc = 1

        getVersionName = { suffix = '' ->
            if (branch.equals('master')) {
                return "${versionName}${suffix}"
            } else if (branch.startsWith('release/')) {
                "${versionName}${suffix}-RC1"
            } else {
                "${versionName}${suffix}-SNAPSHOT-${sha}"
            }
        }
    }

    repositories {
        google()
        jcenter()
        mavenCentral()
        flatDir {
            dirs 'libs'
        }
    }
}
