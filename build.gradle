// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
    }
}

apply from: 'dependencies.gradle'

subprojects {

    tasks.withType(Test) {
        testLogging {
            events "started", "passed", "skipped", "failed"
        }
    }
}

allprojects {

    project.ext {
        apiVersion = "2.0.0"
        apiVersionCode = 2000
        apiRc = 1

        getVersionName = { suffix = '' ->
            return apiRc == 0 ? "${apiVersion}${suffix}" : "${apiVersion}${suffix}-RC${apiRc}"
        }
    }

    repositories {
        google()
        jcenter()
        mavenCentral()
        maven {
            url "s3://sdk-releases.aevi.com/maven2"
            credentials(AwsCredentials) {
                accessKey aws_accessid
                secretKey aws_accesskey
            }
        }
        flatDir {
            dirs 'libs'
        }
    }
}
