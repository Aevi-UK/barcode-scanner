// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.2'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

apply from: 'dependencies.gradle'

subprojects {

    tasks.withType(Test) {
        testLogging {
            events "started", "passed", "skipped", "failed"
        }
    }
}

allprojects {

    project.ext {
        branch = 'git rev-parse --abbrev-ref HEAD'.execute([], project.rootDir).text.trim()
        sha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
        apiVersion = "2.1.0"
        apiVersionCode = 2006
        apiRc = 1

        getVersionName = { suffix = '' ->
            if (branch.equals('master')) {
                return "${apiVersion}${suffix}"
            } else if (branch.startsWith('release/')) {
                "${apiVersion}${suffix}-RC1"
            } else {
                "${apiVersion}${suffix}-SNAPSHOT-${sha}"
            }
        }
    }

    repositories {
        google()
        jcenter()
        mavenCentral()
        flatDir {
            dirs 'libs'
        }
    }
}
